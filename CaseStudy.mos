model "The 3-Echelon Multi-period Warehouse Location Problem with Uncertainty"
uses "mmsystem", "mmjobs", "mmxprs", "mmxnlp"
setparam("XPRS_VERBOSE", true)

! ============================================================================
! Reading problem parameters
! ============================================================================
filename := "CaseStudyData.txt";

declarations
  ! Number of customers
  nbCustomers: integer
  ! Number of candidate locations
  nbCandidates: integer
  ! Number of suppliers
  nbSuppliers: integer
  ! Number of product groups
  nbProductGroups: integer
  ! Number of vehicle types
  nbVehicleTypes: integer
  ! Number of periods
  nbPeriods: integer
  ! Number of scenarios
  nbScenarios: integer
end-declarations

! Read the various numbers from the file
initializations from filename
  nbCustomers nbCandidates nbSuppliers nbProductGroups nbVehicleTypes
  nbPeriods nbScenarios
end-initializations

declarations
  ! Set of customers
  Customers = 1..nbCustomers
  ! Set of candidate locations
  Candidates = 1..nbCandidates
  ! Set of suppliers
  Suppliers = 1..nbSuppliers
  ! Set of product groups
  Products = 1..nbProductGroups
  ! Set of vehicle types
  Vehicles = 1..nbVehicleTypes
  ! Set of periods
  Periods = 1..nbPeriods
  ! Set of scenarios
  Scenarios = 1..nbScenarios

  ! Vector of customer ids
  CustomerId: array(Customers) of string
  ! Vectors of customer coordinates
  CustomerEasting: array(Customers) of real
  CustomerNorthing: array(Customers) of real

  ! The overall customer demand in kilograms per product group
  Demand: array(Customers, Products) of integer
  ! The annual customer demand in kilograms per product group and period
  DemandPeriods: array(Customers, Products, Periods) of integer
  ! The annual customer demand in kilograms per product group, period, and scenario
  DemandPeriodScenarios: array(Customers, Products, Periods, Scenarios) of integer

  ! Vector of candidate ids
  CandidateId: array(Candidates) of string
  ! Vectors of candidate coordinates
  CandidateEasting: array(Candidates) of real
  CandidateNorthing: array(Candidates) of real

  ! Vector of supplier ids
  SupplierId: array(Suppliers) of integer
  ! Vectors of supplier coordinates
  SupplierEasting: array(Suppliers) of real
  SupplierNorthing: array(Suppliers) of real
  ! Vector of supplier product groups; recall that each supplier only provides one product group
  SupplierProductGroup: array(Suppliers) of integer
  ! Vector of supplier capacity given in kilograms per year
  SupplierCapacity: array(Suppliers) of real
  ! Vector of supplier vehicle types
  SupplierVehicleType: array(Suppliers) of integer

  ! Setup costs for warehouses
  Setup: array(Candidates) of integer
  ! Operating costs for warehouses
  Operating: array(Candidates) of integer
  ! Capacity for warehouses
  Capacity: array(Candidates) of integer

  ! Distance matrix between candidate locations and customers
  DistanceCandidateCustomer: array(Candidates,Customers) of real
  ! Distance matrix between candidate locations and suppliers
  DistanceCandidateSupplier: array(Candidates,Suppliers) of real
  ! Matrix of transportation costs between candidate locations and customers
  CostCandidateCustomers: array(Candidates,Customers) of real
  ! Matrix of transportation costs between candidate locations and suppliers
  CostCandidateSupplier: array(Candidates,Suppliers) of real

  ! Vehicle related data. The vehicles are indexed 1, 2, and 3, where 1 stands
  ! for 18t trucks, 2 for 7.5t lorries, and 3 for 3.5t vans.
  ! The vehicle capacity in tonnes
  VehicleCapacity: array(Vehicles) of real
  ! The overall cost in pounds per mile travelled
  VehicleCostPerMileOverall: array(Vehicles) of real
  ! The overall cost in pounds per mile and tonne transported
  VehicleCostPerMileAndTonneOverall: array(Vehicles) of real
  ! The CO2 emission in kilograms per mile and tonne transported
  VehicleCO2PerMileAndTonne: array(Vehicles) of real

  !We now define decision variables

  !define the demand of product served by candidate loc to customer in period
  x: array(Customers, Candidates, Periods, Products) of mpvar

  !where to set up location of warehouse
  y: array(Candidates, Periods) of mpvar

  !number of demand units that s supplies to j in time period t
  z: array(Candidates, Suppliers, Periods) of mpvar

end-declarations

! Read data
initializations from filename
  CustomerId CustomerEasting CustomerNorthing
  CandidateId CandidateEasting CandidateNorthing
  SupplierId SupplierEasting SupplierNorthing
  SupplierProductGroup SupplierCapacity SupplierVehicleType
  Setup Operating Capacity
  Demand as "CustomerDemand"
  DemandPeriods as "CustomerDemandPeriods"
  DemandPeriodScenarios as "CustomerDemandPeriodScenarios"
  DistanceCandidateSupplier DistanceCandidateCustomer
  VehicleCapacity VehicleCostPerMileOverall VehicleCostPerMileAndTonneOverall
end-initializations


! ==================================================================================================
! Data preparation
! ==================================================================================================

! Transports between suppliers and locations use either 7.5t or 18t trucks, depending on the
! vehicle type. Make sure to convert from tonne to kilogram.
! --------------------------------------------------------------------------------------------------
forall(j in Candidates, k in Suppliers) do
  if(SupplierVehicleType(k) = 1) then
    CostCandidateSupplier(j,k) := 2 * DistanceCandidateSupplier(j,k) * VehicleCostPerMileAndTonneOverall(1) / 1000
  else
    CostCandidateSupplier(j,k) := 2 * DistanceCandidateSupplier(j,k) * VehicleCostPerMileAndTonneOverall(2) / 1000
  end-if
end-do

! Transports between locations and customers use 3.5t vans.
! Make sure to convert from tonne to kilogram.
! --------------------------------------------------------------------------------------------------
forall(j in Candidates, i in Customers)
  CostCandidateCustomers(j,i) := 2 * DistanceCandidateCustomer(j,i) * VehicleCostPerMileAndTonneOverall(3) / 1000


! ==================================================================================================
! Constraints
! ==================================================================================================

!Binary constraints

forall (can in Candidates, per in Periods, supl in Suppliers)  do
  y(can, per) is_binary
  z(can, supl, per) is_integer
end-do

!Constraints
forall(cust in Customers, can in Candidates, per in Periods, pro in Products) do
  one(cust, can, per, pro) := x(cust, can, per, pro) <= y(can, per)
end-do

forall (cust in Customers, per in Periods, pro in Products) do
  two(cust, per, pro) := sum (can in Candidates) x(cust, can, per, pro) = 1
end-do

forall (supl in Suppliers, per in Periods) do
  three(supl, per) := sum(can in Candidates) z(can, supl, per) <= SupplierCapacity(supl)
end-do

forall (can in Candidates, supl in Suppliers, per in Periods) do
  four(can, supl, per) := z(can, supl, per) <= SupplierCapacity(supl)*y(can, per)
end-do

forall (per in Periods, can in Candidates) do
  if per > 1 then
    five(per, can) := y(can, per) >= fmax(0, y(can, per-1))
  end-if
end-do



! ==================================================================================================
! Optimization
! ==================================================================================================

!costs of warehouse (setup + operation)
forall (can in Candidates) do
  SetupCostPerWH(can) := fmin(1, sum(per in Periods) y(can, per))*Setup(can)
  OperationalCostPerWH(can) := sum(per in Periods) y(can, per)*Operating(can)
end-do

TotalSetupCost := sum(can in Candidates) SetupCostPerWH(can)
TotalOperationCostofWH := sum(can in Candidates) OperationalCostPerWH(can)

!Transports costs
TotalSuppliersCost := sum(sup in Suppliers, can in Candidates, per in Periods) z(can, sup, per)*CostCandidateSupplier(can, sup)

TotalCustomerDeliveryCost := sum(cust in Customers, can in Candidates, per in Periods, pro in Products) CostCandidateCustomers(can, cust)*x(cust, can, per, pro)*DemandPeriods(cust, pro, per)

TotalCost := TotalSetupCost + TotalOperationCostofWH + TotalSuppliersCost + TotalCustomerDeliveryCost

minimise(TotalCost)


! ==================================================================================================
! Results
! ==================================================================================================

writeln("TotalCost: ", getobjval)
end-model
